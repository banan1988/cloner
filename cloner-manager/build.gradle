buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.netflix.nebula:gradle-ospackage-plugin:5.0.2"
    }
}

plugins {
    id "nebula.ospackage" version "5.0.2" apply true
}

ospackage {
    packageName = osPackageName
    version = osPackageVersion
    release = osPackageRelease
    arch = X86_64
    os = LINUX
    user = 'root'
    permissionGroup = 'root'
    fileMode 0644

    // scripts
    preInstall file('scripts/preInstall.sh')
    postInstall file('scripts/postInstall.sh')
    preUninstall file('scripts/preUninstall.sh')
    postUninstall file('scripts/postUninstall.sh')

    // bin
    from("cloner") {
        into '/opt/cloner-manager'
        user 'cloner-manager'
        permissionGroup 'cloner-manager'
    }
    link("/usr/bin/cloner-manager", "/opt/cloner-manager/cloner-manager.py", 0755)

    // config
    from("config/config.json") {
        into '/etc/cloner-manager.d'
        fileType CONFIG | NOREPLACE

        user 'cloner-manager'
        permissionGroup 'cloner-manager'
        fileMode 0644
    }

    // systemd service
    from("service/cloner-manager.service") {
        into '/usr/lib/systemd/system'
        fileType CONFIG | NOREPLACE

        user 'root'
        permissionGroup 'root'
        fileMode 0644
    }
}

buildRpm {
    user = 'cloner-manager'
    permissionGroup = 'cloner-manager'

    addParentDirs true

    // requires
    requires('python34')
    requires('python34-virtualenv')

    // directories
    directory('/etc/cloner-manager.d', 0644)
    directory('/opt/cloner-manager', 0644)
    directory('/var/cloner-manager', 0755)
    directory('/var/log/cloner-manager', 0755)
}

buildDeb {
    user = 'cloner-manager'
    permissionGroup = 'cloner-manager'

    addParentDirs true

    // requires
    requires('python3.4')
    requires('python3.4-virtualenv')

    // directories
    directory('/etc/cloner-manager.d', 0644)
    directory('/opt/cloner-manager', 0644)
    directory('/var/cloner-manager', 0755)
    directory('/var/log/cloner-manager', 0755)
}
