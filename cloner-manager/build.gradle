buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.netflix.nebula:gradle-ospackage-plugin:5.0.4"
    }
}

plugins {
    id "nebula.ospackage" version "5.0.4" apply true
}

ospackage {
    packageName = osPackageName
    version = osPackageVersion
    release = osPackageRelease
    os = LINUX
    user = 'root'
    permissionGroup = 'root'
    fileMode 0644

    // scripts
    preInstall file('scripts/preInstall.sh')
    preUninstall file('scripts/preUninstall.sh')
    postUninstall file('scripts/postUninstall.sh')

    // bin
    from("cloner/cloner-manager.py") {
        into '/opt/cloner-manager'
        fileType CONFIG | NOREPLACE

        user 'cloner-manager'
        permissionGroup 'cloner-manager'
        fileMode 0755
    }
    link("/usr/bin/cloner-manager", "/opt/cloner-manager/cloner-manager.py", 0755)

    // lib
    from("cloner") {
        into '/opt/cloner-manager'
        exclude 'cloner-manager.py'

        user 'cloner-manager'
        permissionGroup 'cloner-manager'
    }

    // config
    from("config/config.json") {
        into '/etc/cloner-manager.d'
        fileType CONFIG | NOREPLACE

        user 'cloner-manager'
        permissionGroup 'cloner-manager'
        fileMode 0644
    }

    // init service
    from("service/cloner-manager") {
        into '/etc/init.d'
        fileType CONFIG | NOREPLACE

        user 'root'
        permissionGroup 'root'
        fileMode 0755
    }

    // systemd service
    from("service/cloner-manager.service") {
        into '/usr/lib/systemd/system'
        fileType CONFIG | NOREPLACE

        user 'root'
        permissionGroup 'root'
        fileMode 0644
    }
}

buildRpm {
    arch = 'X86_64'

    user = 'cloner-manager'
    permissionGroup = 'cloner-manager'

    addParentDirs true

    // requires
    requires('python34')
    requires('python34-virtualenv')

    // directories
    directory('/etc/cloner-manager.d', 0644)
    directory('/opt/cloner-manager', 0644)
    directory('/var/cloner-manager', 0755)
    directory('/var/log/cloner-manager', 0755)

    // scripts
    postInstall file('scripts/rpm/postInstall.sh')
}

buildDeb {
    arch = 'amd64'

    user = 'cloner-manager'
    permissionGroup = 'cloner-manager'

    addParentDirs true

    // requires
    requires('python3.4')
    requires('python3.4-venv')

    // directories
    directory('/etc/cloner-manager.d', 0644)
    directory('/opt/cloner-manager', 0644)
    directory('/var/cloner-manager', 0755)
    directory('/var/log/cloner-manager', 0755)

    // scripts
    postInstall file('scripts/deb/postInstall.sh')
}

task removeDebChanges(type: Delete) {
    dependsOn buildDeb

    delete "${buildDir}/distributions/${osPackageName}_${osPackageVersion}-${osPackageRelease}_amd64.changes"
    followSymlinks = true
}
