buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.netflix.nebula:gradle-ospackage-plugin:5.0.2"
    }
}

plugins {
    id "nebula.ospackage" version "5.0.2" apply true
    id "de.undercouch.download" version "3.4.3" apply true
}

// download bin
task downloadZipFile(type: Download) {
    src "https://github.com/buger/goreplay/releases/download/v${gorPackageVersion}/gor_${gorPackageVersion}_x64.tar.gz"
    dest new File("${projectDir}/.download", "gor_${gorPackageVersion}_x64.tar.gz")
    overwrite false
}

// unpack bin
task downloadAndUnzipFile(dependsOn: downloadZipFile, type: Copy) {
    from tarTree(downloadZipFile.dest)
    into buildDir
}

ospackage {
    packageName = osPackageName
    version = osPackageVersion
    release = osPackageRelease
    arch = X86_64
    os = LINUX
    user = 'root'
    permissionGroup = 'root'
    fileMode 0644

    // scripts
    preInstall file('scripts/preInstall.sh')
    postInstall file('scripts/postInstall.sh')
    preUninstall file('scripts/preUninstall.sh')
    postUninstall file('scripts/postUninstall.sh')

    // gor bin
    from("${buildDir}/goreplay") {
        into '/opt/cloner-service'
        fileType CONFIG | NOREPLACE

        user 'cloner-service'
        permissionGroup 'cloner-service'
        fileMode 0750
    }

    // bin
    from("cloner") {
        into '/opt/cloner-service'
        user 'cloner-service'
        permissionGroup 'cloner-service'
    }
    link("/usr/bin/cloner-service", "/opt/cloner-service/cloner-service.py", 0755)

    // config
    from("config/config.json") {
        into '/etc/cloner-service.d'
        fileType CONFIG | NOREPLACE

        user 'cloner-service'
        permissionGroup 'cloner-service'
        fileMode 0644
    }

    // systemd service
    from("service/cloner-service.service") {
        into '/usr/lib/systemd/system'
        fileType CONFIG | NOREPLACE

        user 'root'
        permissionGroup 'root'
        fileMode 0644
    }
}

buildRpm {
    dependsOn downloadAndUnzipFile

    user = 'cloner-service'
    permissionGroup = 'cloner-service'

    addParentDirs true

    // requires
    requires('python34')
    requires('python34-virtualenv')

    // directories
    directory('/etc/cloner-service.d', 0644)
    directory('/opt/cloner-service', 0644)
    directory('/var/cloner-service', 0755)
    directory('/var/log/cloner-service', 0755)
}

buildDeb {
    user = 'cloner-service'
    permissionGroup = 'cloner-service'

    addParentDirs true

    // requires
    requires('python3.4')
    requires('python3.4-virtualenv')

    // directories
    directory('/etc/cloner-service.d', 0644)
    directory('/opt/cloner-service', 0644)
    directory('/var/cloner-service', 0755)
    directory('/var/log/cloner-service', 0755)
}
